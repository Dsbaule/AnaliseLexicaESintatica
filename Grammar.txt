<program> ::= <block>
<block> ::= { <decls> <stmts> }
<decls> ::= <decl> <decls> | &
<decl> ::= <type> id ;
<type> ::= basic <types>
<types> ::= [ num ] <types> | &
<stmts> ::= <stmt> <stmts> | &
<stmt> ::= <loc> = <bool> ; | <if> | while ( <bool> ) <stmt> | do <stmt> while ( <bool> ) ; | break ; | <block>
<if> ::= if ( <bool> ) then <stmt> else <stmt>
<loc> ::= id <locs>
<locs> ::= [ <bool> ] <locs> | &
<bool> ::= <join> | <join> || <bool>
<join> ::= <equality> | <equality> && <join>
<equality> ::= <rel> | <rel> == <equality> | <rel> != <equality>
<rel> ::= <expr> < <expr> | <expr> <= <expr> | <expr> >= <expr> | <expr> > <expr> | <expr>
<expr> ::= <term> <exprs>
<exprs> ::= + <term> <exprs> | - <term> <exprs> | &
<term> ::= <unary> <terms>
<terms> ::= * <unary> <terms> | / <unary> <terms> | &
<unary> ::= ! <unary> | - <unary> | <factor>
<factor> ::= ( <bool> ) | <loc> | num | real | true | false
